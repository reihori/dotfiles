#=============================
# env
#=============================

eval "$(dircolors -b)"

HISTFILE="$HOME/.zsh_history"
HISTSIZE=10000
SAVEHIST=10000

#=============================
# option
#=============================

setopt AUTO_CD
setopt HIST_IGNORE_ALL_DUPS
setopt HIST_IGNORE_SPACE
setopt HIST_REDUCE_BLANKS

#=============================
# function
#=============================

function chpwd() {
    command ls --color=auto --group-directories-first
}

function append-pipe-less() {
    if [[ "$LBUFFER" != *"| less"* ]]; then
        LBUFFER="$LBUFFER | less"
    fi
}
zle -N append-pipe-less

#=============================
# bindkey
#=============================

stty stop undef
bindkey -e
bindkey '^Xl' append-pipe-less

#=============================
# completion
#=============================

fpath=("$XDG_DATA_HOME/zsh/site-functions" $fpath)
typeset -U fpath FPATH

zstyle ':completion:*' menu select
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}'
zstyle ':completion:*' list-colors ${(s.:.)LS_COLORS}

autoload -Uz compinit
compinit

#=============================
# alias
#=============================

typeset -A my_aliases
my_aliases=(
    c 'cd'
    g 'git'
    ga 'git add'
    gaa 'git add --all'
    gau 'git add --update'
    gb 'git branch'
    gc 'git commit'
    gd 'git diff'
    gds 'git diff --staged'
    gl 'git log'
    glg 'git log --oneline --all --graph'
    glo 'git log --oneline'
    glp 'git log --patch'
    gr 'git restore'
    gs 'git status'
    gw 'git switch'
    h 'history'
    l 'ls'
    la 'ls -A'
    ll 'ls -lh'
    lla 'ls -lhA'
    ls 'ls --color=auto --group-directories-first'
)

for key val in ${(kv)my_aliases}; do
    alias "$key"="$val"
done

#=============================
# tool
#=============================

if command -v sheldon &> /dev/null; then
    eval "$(sheldon source)"
fi

if command -v starship &> /dev/null; then
    export STARSHIP_CONFIG="$XDG_CONFIG_HOME/starship/starship.toml"
    eval "$(starship init zsh)"
fi

if [[ -f "$HOME/.cargo/env" ]]; then
    . "$HOME/.cargo/env"
fi

if command -v mise &> /dev/null; then
    eval "$(mise activate zsh)"
fi

if command -v zoxide &> /dev/null; then
    eval "$(zoxide init zsh)"
fi

if command -v eza &> /dev/null; then
    alias eza='eza --group-directories-first --time-style long-iso'
    alias l='eza'
    alias la='eza -a'
    alias ll='eza -lg'
    alias lla='eza -alg'
    alias ls='eza'
    alias lt='eza --tree'
fi

if command -v bat &> /dev/null; then
    alias b='bat'
fi

if command -v vim &> /dev/null; then
    export EDITOR=vim
    alias v='vim'
fi

# The abbr setting must be the last one.
if command -v abbr &> /dev/null; then
    abbr import-aliases --session --quiet
fi
